// Sacamos los metodos de parseo fuera del pipeline 
def parseXml(String xmlContent) {
    def parsedXml = new XmlSlurper().parseText(xmlContent)
    def testSuitesAttributes = [
        name: parsedXml.'@name'.text(),
        totalTime: parsedXml.'@time'.text().toFloat(),
        totalTests: parsedXml.'@tests'.text().toInteger(),
        totalFailures: parsedXml.'@failures'.text().toInteger(),
        totalErrors: parsedXml.'@errors'.text().toInteger()
    ]

    def testSuites = []

    // Iterar sobre cada <testsuite>
    parsedXml.'testsuite'.each { suite ->
        def suiteMetrics = [
            name: suite.'@name'.text(),
            timestamp: suite.'@timestamp'.text(),
            totalTime: suite.'@time'.text().toFloat(),
            totalTests: suite.'@tests'.text().toInteger(),
            skipped: suite.'@skipped'.text().toInteger(),
            totalFailures: suite.'@failures'.text().toInteger(),
            totalErrors: suite.'@errors'.text().toInteger()
        ]
        testSuites.add(suiteMetrics)
    }
    return [testSuitesAttributes: testSuitesAttributes, testSuites: testSuites]
}

def constructPrometheusData(Map parsedData) {
    // Attributes from the root 'testsuites' element
    def testSuitesAttributes = parsedData.testSuitesAttributes
    // List of parsed 'testsuite' elements
    def testSuites = parsedData.testSuites

    // Construct Prometheus data for 'testsuites' level
    def prometheusData = """
# HELP katalon_testsuites_total Total de pruebas para todos los testsuites
# TYPE katalon_testsuites_total gauge
katalon_testsuites_total{name="${testSuitesAttributes.name}"} ${testSuitesAttributes.totalTests}

# HELP katalon_testsuites_failures Total de fallos para todos los testsuites
# TYPE katalon_testsuites_failures gauge
katalon_testsuites_failures{name="${testSuitesAttributes.name}"} ${testSuitesAttributes.totalFailures}

# HELP katalon_testsuites_errors Total de errores para todos los testsuites
# TYPE katalon_testsuites_errors gauge
katalon_testsuites_errors{name="${testSuitesAttributes.name}"} ${testSuitesAttributes.totalErrors}

# HELP katalon_testsuites_time Tiempo total de ejecución para todos los testsuites
# TYPE katalon_testsuites_time gauge
katalon_testsuites_time{name="${testSuitesAttributes.name}"} ${testSuitesAttributes.totalTime}
"""

    // Append Prometheus data for each 'testsuite' level
    testSuites.each { suite ->
        prometheusData += """
# HELP katalon_testsuite_info Información de la suite de pruebas individual
# TYPE katalon_testsuite_info gauge
katalon_testsuite_info{name="${suite.name}", timestamp="${suite.timestamp}", total_tests="${suite.totalTests}", skipped="${suite.skipped}", total_failures="${suite.totalFailures}", total_errors="${suite.totalErrors}", total_time="${suite.totalTime}"} 1

# HELP katalon_testsuite_duration Duración de la suite de pruebas individual
# TYPE katalon_testsuite_duration gauge
katalon_testsuite_duration{name="${suite.name}", timestamp="${suite.timestamp}"} ${suite.totalTime}

# HELP katalon_testsuite_tests Total de pruebas en la suite de pruebas individual
# TYPE katalon_testsuite_tests gauge
katalon_testsuite_tests{name="${suite.name}", timestamp="${suite.timestamp}"} ${suite.totalTests}

# HELP katalon_testsuite_skipped Total de pruebas omitidas en la suite de pruebas individual
# TYPE katalon_testsuite_skipped gauge
katalon_testsuite_skipped{name="${suite.name}", timestamp="${suite.timestamp}"} ${suite.skipped}

# HELP katalon_testsuite_failures Total de fallos en la suite de pruebas individual
# TYPE katalon_testsuite_failures gauge
katalon_testsuite_failures{name="${suite.name}", timestamp="${suite.timestamp}"} ${suite.totalFailures}

# HELP katalon_testsuite_errors Total de errores en la suite de pruebas individual
# TYPE katalon_testsuite_errors gauge
katalon_testsuite_errors{name="${suite.name}", timestamp="${suite.timestamp}"} ${suite.totalErrors}
"""
    }

    return prometheusData
}

pipeline {
    agent {
        kubernetes {
            defaultContainer 'jdk'
            yaml '''
apiVersion: v1
kind: Pod
spec:
  containers:
    - name: jdk
      image: docker.io/eclipse-temurin:20.0.1_9-jdk
      command:
        - cat
      tty: true
      volumeMounts:
        - name: m2-cache
          mountPath: /root/.m2
    - name: podman
      image: quay.io/containers/podman:v4.5.1
      command:
        - cat
      tty: true
      securityContext:
        runAsUser: 0
        privileged: true
    - name: kubectl
      image: docker.io/bitnami/kubectl:1.27.3
      command:
        - cat
      tty: true
      securityContext:
        runAsUser: 0
        privileged: true
  volumes:
    - name: m2-cache
      hostPath:
        path: /data/m2-cache
        type: DirectoryOrCreate
    - name: shared-data
      hostPath:
        path: /path/to/shared/folder
        type: DirectoryOrCreate
'''
        }
    }

    environment {
          PUSHGATEWAY_URL = 'http://pushgateway-prometheus-pushgateway.default.svc.cluster.local:9091/metrics/job/katalon' 
    }

    stages {
       
        stage('Parse Katalon XML') {
            steps {
                script {
                    // katalon.xml está en el directorio actual
                    def katalonXml = readFile('katalon.xml')

                    // parseXml para parsear el XML
                    def metrics = parseXml(katalonXml)

                    // Imprimir los resultados en la consola
                    echo "Metrics parsed: ${metrics}"

                    // Construye la cadena de datos para Prometheus con las métricas
                    def prometheusData = constructPrometheusData(metrics)

                    // Escribe las métricas en un archivo
                    writeFile(file: 'katalon.prom', text: prometheusData)

                    // imprimimos el contenido de 'prometheusData'
                    echo "Prometheus data:\n${prometheusData}"
                }
            }
        }

        stage('Send to Prometheus') {
              steps {
                  script {
                      
                      def prometheusData = readFile('katalon.prom').trim()
                      if (prometheusData) {
                        //def pushgatewayHost = '127.0.0.1'
                        //def pushgatewayPort = '9091' 
                        def pushgatewayUrl = env.PUSHGATEWAY_URL
                        def response = sh(script: "curl -X POST --data-binary @katalon.prom ${pushgatewayUrl}", returnStdout: true).trim()
                        echo "Response from Pushgateway: ${response}"
                      } else {
                          error("Prometheus data está vacío, no envía al Pushgateway")
                      }
                  }
              }
          }
    }
}
