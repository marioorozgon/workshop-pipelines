// Sacamos los metodos de parseo fuera del pipeline 
//TODO ver si se puede pasar a una propia clase groovy

def parseXml(String xmlContent) {
    def parsedXml = new XmlSlurper().parseText(xmlContent)
    
    def suites = parsedXml.'testsuite'.collect { suite ->
        def suiteData = [
            name: suite.'@name'.text(),
            timestamp: suite.'@timestamp'.text(),
            time: suite.'@time'.text().toFloat(),
            tests: suite.'@tests'.text().toInt(),
            skipped: suite.'@skipped'.text().toInt(),
            failures: suite.'@failures'.text().toInt(),
            errors: suite.'@errors'.text().toInt(),
            testcases: []
        ]
        
        suite.testcase.each { tc ->
            def testcaseData = [
                name: tc.'@name'.text(),
                time: tc.'@time'.text().toFloat(),
                status: tc.'@status'?.text() ?: 'OK',
                failure: null,
                error: null
            ]
            
            if (tc.failure.size() > 0) {
                testcaseData.failure = [
                    message: tc.failure.'@message'.text(),
                    type: tc.failure.'@type'.text()
                ]
            }
            
            if (tc.error.size() > 0) {
                testcaseData.error = [
                    message: tc.error.'@message'.text(),
                    type: tc.error.'@type'.text()
                ]
            }
            
            suiteData.testcases << testcaseData
        }
        
        return suiteData
    }
    
    return suites
}

def constructPrometheusData(List suiteMetrics) {
    def prometheusData = new StringBuilder()
    
    suiteMetrics.each { suite ->
        suite.testcases.each { testcase ->
            def statusValue = (testcase.status == 'OK') ? 1 : 0
            prometheusData.append("""
# HELP katalon_testcase_duration_seconds Duration of testcase in seconds
# TYPE katalon_testcase_duration_seconds gauge
katalon_testcase_duration_seconds{name="${testcase.name}", status="${testcase.status}"} ${testcase.time}

# HELP katalon_testcase_status Testcase status
# TYPE katalon_testcase_status gauge
katalon_testcase_status{name="${testcase.name}", message="${testcase.failure?.message ?: testcase.error?.message ?: 'OK'}"} $statusValue
""")
        }
    }
    
    return prometheusData.toString()
}

pipeline {
    agent {
        kubernetes {
            defaultContainer 'jdk'
            yaml '''
apiVersion: v1
kind: Pod
spec:
  containers:
    - name: jdk
      image: docker.io/eclipse-temurin:20.0.1_9-jdk
      command:
        - cat
      tty: true
      volumeMounts:
        - name: m2-cache
          mountPath: /root/.m2
    - name: podman
      image: quay.io/containers/podman:v4.5.1
      command:
        - cat
      tty: true
      securityContext:
        runAsUser: 0
        privileged: true
    - name: kubectl
      image: docker.io/bitnami/kubectl:1.27.3
      command:
        - cat
      tty: true
      securityContext:
        runAsUser: 0
        privileged: true
  volumes:
    - name: m2-cache
      hostPath:
        path: /data/m2-cache
        type: DirectoryOrCreate
    - name: shared-data
      hostPath:
        path: /path/to/shared/folder
        type: DirectoryOrCreate
'''
        }
    }

    environment {
          PUSHGATEWAY_URL = 'http://localhost:9091' 
    }

    stages {
       
        stage('Parse Katalon XML') {
            steps {
                script {
                    // katalon.xml está en el directorio actual
                    def katalonXml = readFile('JUnit_Report_GTI_ERROR.xml')

                    // parseXml para parsear el XML
                    def metrics = parseXml(katalonXml)

                    // Imprimir los resultados en la consola
                    echo "Metrics parsed: ${metrics}"

                    // Construye la cadena de datos para Prometheus con las métricas
                    def prometheusData = constructPrometheusData(metrics)

                    // Escribe las métricas en un archivo
                    writeFile(file: 'katalon.prom', text: prometheusData)

                    // imprimimos el contenido de 'prometheusData'
                    echo "Prometheus data:\n${prometheusData}"
                }
            }
        }

        stage('Send to Prometheus') {
              steps {
                  script {
                      
                      def prometheusData = readFile('katalon.prom').trim()
                      if (prometheusData) {
                        //def pushgatewayHost = '127.0.0.1'
                        //def pushgatewayPort = '9091' 
                        def pushgatewayUrl = "http://pushgateway-prometheus-pushgateway.default.svc.cluster.local:9091/metrics/job/katalon"
                        def response = sh(script: "curl -X POST --data-binary @katalon.prom ${pushgatewayUrl}", returnStdout: true).trim()
                        echo "Response from Pushgateway: ${response}"
                      } else {
                          error("Prometheus data está vacío, no envía al Pushgateway")
                      }
                  }
              }
          }
    }
}
